rank=config["target_rank"]
community=config['community_name']
tools=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon']
vir_threshold=config["vir_reads_threshold"]
bac_threshold=config["bac_reads_threshold"]


rule all_tax:
    input: merged_tsv=expand("taxonomy-tables/{tool}/all_{tool}.tsv",tool=tools)

rule all_tools:
    input: merged_tsv=expand("taxonomy-tables/{tool}/all_{tool}.tsv",tool=tools),
           vir_heatmap_per_sample=expand("plots/heatmaps/{tool}/viruses/{rank}_level.pdf",rank=rank,tool=tools),
           bac_heatmap_per_sample=expand("plots/heatmaps/{tool}/bacteria/{rank}_level.pdf",rank=rank,tool=tools),
           barplots=expand("plots/barplots/all_tools/{superkingdom}/{rank}_level.pdf",rank=rank,superkingdom=['viruses','bacteria']),
           vir_heatmap_counts=expand("plots/heatmaps/all_tools/{superkingdom}/counts_{threshold}_{rank}_level.pdf",rank=rank,superkingdom='viruses',threshold=vir_threshold),
           bac_heatmap_counts=expand("plots/heatmaps/all_tools/{superkingdom}/counts_{threshold}_{rank}_level.pdf",rank=rank,superkingdom='bacteria',threshold=bac_threshold),
           heatmap_presence=expand("plots/heatmaps/all_tools/{superkingdom}/presence_{rank}_level.pdf",rank=rank,superkingdom=['viruses','bacteria']),
           tab="compare_tools/resources_usage/resources.tsv",
           memo="compare_tools/resources_usage/memory.pdf",
           time="compare_tools/resources_usage/time.pdf"

rule all_benchmark:
    input:  merged_tsv=expand("taxonomy-tables/{tool}/all_{tool}.tsv",tool=tools),
            vir_read_counts_heatmaps=expand("plots/heatmaps/{tool}/viruses/{rank}_level.pdf",rank=rank,tool=tools),
            bac_read_counts_heatmaps=expand("plots/heatmaps/{tool}/bacteria/{rank}_level.pdf",rank=rank,tool=tools),
            gold_standard=f"gold_standard/tables/gs_{community}.tsv",
            all_vir_scores=f"compare_tools/{community}/classification/stats/all_tools/vir_scores_{rank}_level.tsv",
            all_vir_presence=f"compare_tools/{community}/classification/stats/all_tools/vir_presence_{rank}_level.tsv",
            all_bac_scores=f"compare_tools/{community}/classification/stats/all_tools/bac_scores_{rank}_level.tsv",
            all_bac_presence=f"compare_tools/{community}/classification/stats/all_tools/bac_presence_{rank}_level.tsv",
            vir_presence_heatmap=f"compare_tools/{community}/classification/heatmaps/all_tools/virus_{rank}_level.pdf",
            bac_presence_heatmap=f"compare_tools/{community}/classification/heatmaps/all_tools/bacteria_{rank}_level.pdf",
            barplots=expand("compare_tools/{community}/classification/plots/{tool}/barplot_{rank}_level.pdf",community=community,tool=tools,rank=rank),
            tab="compare_tools/resources_usage/resources.tsv",
            memo="compare_tools/resources_usage/memory.pdf",
            time="compare_tools/resources_usage/time.pdf"

rule merge_Kraken2_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("kraken2/{sample}/report.txt",sample=list(read_naming.keys()))

    output: f"taxonomy-tables/kraken2/all_kraken2.tsv"

    script: "scripts/kraken2-merge.py"

rule merge_Bracken_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("kraken2/{sample}/bracken.txt",sample=list(read_naming.keys()))

    output: f"taxonomy-tables/bracken/all_bracken.tsv"

    script: "scripts/bracken-merge.py"

rule merge_Pathseq_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("Pathseq/{sample}/output/scores.txt",sample=list(read_naming.keys()))

    output: f"taxonomy-tables/pathseq/all_pathseq.tsv"

    script: "scripts/pathseq-merge.py"

rule merge_kaiju_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("kaiju/{sample}/summary.tsv",sample=list(read_naming.keys()))

    output: f"taxonomy-tables/kaiju/all_kaiju.tsv"

    log: logging_folder + f"compare_tools/{community}/classification/kaiju.log"

    script: "scripts/kaiju-merge.py"

rule merge_SURPI_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: bac=expand("SURPI/{sample}/OUTPUT_{sample}/{sample}.NT.snap.matched.d1.fl.Bacteria.annotated.species.clx.counttable",sample=list(read_naming.keys())),
           vir=expand("SURPI/{sample}/OUTPUT_{sample}/{sample}.NT.snap.matched.d16.fl.Viruses.filt.NTblastn_tru.dust.annotated.species.clx.counttable",sample=list(read_naming.keys()))
            #dust.annotated file because low complexity regions are substracted

    output: f"taxonomy-tables/surpi/all_surpi.tsv"

    log: logging_folder + f"compare_tools/{community}/classification/surpi.log"

    script: "scripts/surpi-merge.py"


rule merge_ezvir_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("Ezvir/{sample}/report/ALL-RESULTS.ezv",sample=list(read_naming.keys()))

    output: f"taxonomy-tables/ezvir/all_ezvir.tsv"

    params: read_len=config["samples_read_length"]

    script: "scripts/Ezvir-merge.py"


rule merge_ganon_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("ganon/{sample}/classified.rep",sample=list(read_naming.keys()))

    output: f"taxonomy-tables/ganon/all_ganon.tsv"

    script: "scripts/ganon-merge.py"


rule virus_read_count_heatmaps:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: f"taxonomy-tables/{{tool}}/all_{{tool}}.tsv"

    output: f"plots/heatmaps/{{tool}}/viruses/{rank}_level.pdf"

    params: rank=config["target_rank"],
            value=config["target_value"],
            superkingdom="Viruses"

    script: "scripts/heatmap-read-counts.py"

rule bacteria_read_count_heatmaps:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: f"taxonomy-tables/{{tool}}/all_{{tool}}.tsv"

    output: f"plots/heatmaps/{{tool}}/bacteria/{rank}_level.pdf"

    params: rank=config["target_rank"],
            value=config["target_value"],
            superkingdom="Bacteria"

    script: "scripts/heatmap-read-counts.py"



rule visualize_viruses_tools_output:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("taxonomy-tables/{tool}/{sample}.tsv",tool=tools,sample=list(read_naming.keys()))

    output: barplot=f"plots/barplots/all_tools/viruses/{rank}_level.pdf",
            heatmap_counts=f"plots/heatmaps/all_tools/viruses/counts_{vir_threshold}_{rank}_level.pdf",
            heatmap_presence=f"plots/heatmaps/all_tools/viruses/presence_{rank}_level.pdf"

    params: rank=config["target_rank"],
            value=config["target_value"],
            superkingdom="Viruses",
            threshold=vir_threshold

    script: "scripts/visualize-tool-output.py"


rule visualize_bacteria_tools_output:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("taxonomy-tables/{tool}/{sample}.tsv",tool=tools,sample=list(read_naming.keys()))

    output: barplot=f"plots/barplots/all_tools/bacteria/{rank}_level.pdf",
            heatmap_counts=f"plots/heatmaps/all_tools/bacteria/counts_{bac_threshold}_{rank}_level.pdf",
            heatmap_presence=f"plots/heatmaps/all_tools/bacteria/presence_{rank}_level.pdf"

    params: rank=config["target_rank"],
            value=config["target_value"],
            superkingdom="Bacteria",
            threshold=bac_threshold

    script: "scripts/visualize-tool-output.py"


rule visualize_resource_usage:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("compare_tools/resources_usage/{sample}/{tool}.benchmark.txt",sample=list(read_naming.keys()),tool=tools)

    output: tab="compare_tools/resources_usage/resources.tsv",
            memo="compare_tools/resources_usage/memory.pdf",
            time="compare_tools/resources_usage/time.pdf"

    script: "scripts/resources-usage.py"

rule genereate_gold_standard_profile:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: f"gold_standard/tables/{community}.tsv"

    output: f"gold_standard/tables/gs_{community}.tsv"

    script: "scripts/gold-standard-tax.py"



rule calculate_precision_recall_f1_score:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: gold_standard=f"gold_standard/tables/gs_{community}.tsv",
            tool_out=f"taxonomy-tables/{{tool}}/all_{{tool}}.tsv"

    output: vir_scores=f"compare_tools/{community}/classification/stats/{{tool}}/vir_scores_{rank}_level.tsv",
            vir_presence=f"compare_tools/{community}/classification/stats/{{tool}}/vir_presence_{rank}_level.tsv",
            bac_scores=f"compare_tools/{community}/classification/stats/{{tool}}/bac_scores_{rank}_level.tsv",
            bac_presence=f"compare_tools/{community}/classification/stats/{{tool}}/bac_presence_{rank}_level.tsv"

    script: "scripts/stats.py"


rule regroup_scores_and_presence:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: vir_scores=expand("compare_tools/{community}/classification/stats/{tool}/vir_scores_{rank}_level.tsv",community=community,tool=tools,rank=rank),
           vir_presence=expand("compare_tools/{community}/classification/stats/{tool}/vir_presence_{rank}_level.tsv",community=community,tool=tools,rank=rank),
           bac_scores=expand("compare_tools/{community}/classification/stats/{tool}/bac_scores_{rank}_level.tsv",community=community,tool=tools,rank=rank),
           bac_presence=expand("compare_tools/{community}/classification/stats/{tool}/bac_presence_{rank}_level.tsv",community=community,tool=tools,rank=rank)

    output: all_vir_scores=f"compare_tools/{community}/classification/stats/all_tools/vir_scores_{rank}_level.tsv",
            all_vir_presence=f"compare_tools/{community}/classification/stats/all_tools/vir_presence_{rank}_level.tsv",
            all_bac_scores=f"compare_tools/{community}/classification/stats/all_tools/bac_scores_{rank}_level.tsv",
            all_bac_presence=f"compare_tools/{community}/classification/stats/all_tools/bac_presence_{rank}_level.tsv"

    script: "scripts/concat.py"


rule bacteria_presence_hetamap:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: f"compare_tools/{community}/classification/stats/all_tools/bac_presence_{rank}_level.tsv"

    output: f"compare_tools/{community}/classification/heatmaps/all_tools/bacteria_{rank}_level.pdf"

    params: rank=config["target_rank"]

    script: "scripts/heatmap-presence.py"

rule virus_presence_hetamap:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: f"compare_tools/{community}/classification/stats/all_tools/vir_presence_{rank}_level.tsv"

    output: f"compare_tools/{community}/classification/heatmaps/all_tools/virus_{rank}_level.pdf"

    params: rank=config["target_rank"]

    script: "scripts/heatmap-presence.py"

rule barplot_counts_gold_standard:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: gold_standard=f"gold_standard/tables/gs_{community}.tsv",
           tool_out=f"taxonomy-tables/{{tool}}/all_{{tool}}.tsv"

    params: rank=config["target_rank"]

    output: f"compare_tools/{community}/classification/plots/{{tool}}/barplot_{rank}_level.pdf"

    script: "scripts/barplot.py"

