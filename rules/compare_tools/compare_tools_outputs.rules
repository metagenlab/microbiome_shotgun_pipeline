rank=config["target_rank"]
rule all_tools_output:
    input: merged_tsv=expand("benchmarks/classification/tables/{tool}/all_{tool}.tsv",tool=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon']),
            vir_read_counts_heatmaps=expand("benchmarks/classification/heatmaps/{tool}/viruses.pdf",tool=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon']),
            bac_read_counts_heatmaps=expand("benchmarks/classification/heatmaps/{tool}/bacteria.pdf",tool=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon']),
            gold_standard="gold_standard/tables/gold_standard.tsv",
            all_vir_scores=f"benchmarks/classification/stats/all_tools/vir_scores_{rank}_level.tsv",
            all_vir_presence=f"benchmarks/classification/stats/all_tools/vir_presence_{rank}_level.tsv",
            all_bac_scores=f"benchmarks/classification/stats/all_tools/bac_scores_{rank}_level.tsv",
            all_bac_presence=f"benchmarks/classification/stats/all_tools/bac_presence_{rank}_level.tsv"


rule merge_Kraken2_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("kraken2/{sample}/report.txt",sample=list(read_naming.keys()))

    output: "benchmarks/classification/tables/kraken2/all_kraken2.tsv"

    script: "scripts/kraken2-merge.py"

rule merge_Bracken_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("kraken2/{sample}/bracken.txt",sample=list(read_naming.keys()))

    output: "benchmarks/classification/tables/bracken/all_bracken.tsv"

    script: "scripts/bracken-merge.py"

rule merge_Pathseq_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("Pathseq/{sample}/output/scores.txt",sample=list(read_naming.keys()))

    output: "benchmarks/classification/tables/pathseq/all_pathseq.tsv"

    script: "scripts/pathseq-merge.py"

rule merge_kaiju_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("kaiju/{sample}/summary.tsv",sample=list(read_naming.keys()))

    output: "benchmarks/classification/tables/kaiju/all_kaiju.tsv"

    log: logging_folder + "benchmarks/classification/kaiju.log"

    script: "scripts/kaiju-merge.py"

rule merge_SURPI_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: bac=expand("SURPI/{sample}/OUTPUT_{sample}/{sample}.NT.snap.matched.d1.fl.Bacteria.annotated.species.clx.counttable",sample=list(read_naming.keys())),
           vir=expand("SURPI/{sample}/OUTPUT_{sample}/{sample}.NT.snap.matched.d16.fl.Viruses.filt.NTblastn_tru.dust.annotated.species.clx.counttable",sample=list(read_naming.keys()))
            #dust.annotated file because low complexity regions are substracted

    output: "benchmarks/classification/tables/surpi/all_surpi.tsv"

    log: logging_folder + "benchmarks/classification/surpi.log"

    script: "scripts/surpi-merge.py"


rule merge_ezvir_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: expand("Ezvir/{sample}/report/ALL-RESULTS.ezv",sample=list(read_naming.keys()))

    output: "benchmarks/classification/tables/ezvir/all_ezvir.tsv"

    params: read_len=config["samples_read_length"]

    script: "scripts/Ezvir-merge.py"


rule merge_ganon_outputs:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: vir=expand("ganon/{sample}/viruses/classified.rep",sample=list(read_naming.keys())),
           bac=expand("ganon/{sample}/bacteria/classified.rep",sample=list(read_naming.keys()))

    output: "benchmarks/classification/tables/ganon/all_ganon.tsv"

    script: "scripts/ganon-merge.py"



rule virus_read_count_heatmaps:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: "benchmarks/classification/tables/{tool}/all_{tool}.tsv"

    output: "benchmarks/classification/heatmaps/{tool}/viruses.pdf"

    params: rank=config["target_rank"],
            value=config["target_value"],
            superkingdom="Viruses"

    script: "scripts/heatmap-read-counts.py"

rule bacteria_read_count_heatmaps:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: "benchmarks/classification/tables/{tool}/all_{tool}.tsv"

    output: "benchmarks/classification/heatmaps/{tool}/bacteria.pdf"

    params: rank=config["target_rank"],
            value=config["target_value"],
            superkingdom="Bacteria"

    script: "scripts/heatmap-read-counts.py"



community=config['community_name']

rule genereate_gold_standard_profile:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: f"gold_standard/tables/{community}.tsv"

    output: "gold_standard/tables/gold_standard.tsv"

    script: "scripts/profile.py"



rule calculate_precision_recall_f1_score:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: gold_standard="gold_standard/tables/gold_standard.tsv",
            tool_out="benchmarks/classification/tables/{tool}/all_{tool}.tsv"

    output: vir_scores=f"benchmarks/classification/stats/{{tool}}/vir_scores_{rank}_level.tsv",
            vir_presence=f"benchmarks/classification/stats/{{tool}}/vir_presence_{rank}_level.tsv",
            bac_scores=f"benchmarks/classification/stats/{{tool}}/bac_scores_{rank}_level.tsv",
            bac_presence=f"benchmarks/classification/stats/{{tool}}/bac_presence_{rank}_level.tsv"

    script: "scripts/stats.py"


rule regroup_scores_and_presence:
    conda: pipeline_path + "envs/compare-tools.yml"

    input: vir_scores=expand("benchmarks/classification/stats/{tool}/vir_scores_{rank}_level.tsv",tool=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon'],rank=rank),
           vir_presence=expand("benchmarks/classification/stats/{tool}/vir_presence_{rank}_level.tsv",tool=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon'],rank=rank),
           bac_scores=expand("benchmarks/classification/stats/{tool}/bac_scores_{rank}_level.tsv",tool=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon'],rank=rank),
           bac_presence=expand("benchmarks/classification/stats/{tool}/bac_presence_{rank}_level.tsv",tool=['kraken2','bracken','pathseq','kaiju','surpi','ezvir','ganon'],rank=rank)

    output: all_vir_scores=f"benchmarks/classification/stats/all_tools/vir_scores_{rank}_level.tsv",
            all_vir_presence=f"benchmarks/classification/stats/all_tools/vir_presence_{rank}_level.tsv",
            all_bac_scores=f"benchmarks/classification/stats/all_tools/bac_scores_{rank}_level.tsv",
            all_bac_presence=f"benchmarks/classification/stats/all_tools/bac_presence_{rank}_level.tsv"

    script: "scripts/concat.py"



