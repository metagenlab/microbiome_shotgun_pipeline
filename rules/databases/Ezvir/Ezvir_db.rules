db_path_ezvir=config["ezvir_database"]

rule all_ezvir_db:
        input:  f"{db_path_ezvir}/EZV0.1-database.1.bt2",
                f"{db_path_ezvir}/EZV0.1-database.2.bt2",
                f"{db_path_ezvir}/EZV0.1-database.3.bt2",
                f"{db_path_ezvir}/EZV0.1-database.4.bt2",
                f"{db_path_ezvir}/EZV0.1-database.rev.1.bt2",
                f"{db_path_ezvir}/EZV0.1-database.rev.2.bt2",
                f"{db_path_ezvir}/genome_lengths.csv",
                f"{db_path_ezvir}/genome_names.csv",
                f"{db_path_ezvir}/genome_taxids.csv",
                f"{db_path_ezvir}/colours.csv"


rule download_ezVIR_native_database:
    output: f"{db_path_ezvir}/EZV0.1-database.fasta.gz"

    shell:
        """
        wget http://cegg.unige.ch/ezvir/files/EZV0.1-database.fasta.gz -O {output}
        """

rule decompress_ezVIR_database:
    input: f"{db_path_ezvir}/EZV0.1-database.fasta.gz"

    output: f"{db_path_ezvir}/EZV0.1-database.fa"

    shell: "zcat {input} > {output}"



rule index_ezVIR_database:
    conda: pipeline_path + "envs/ezvir.yml"

    input:  f"{db_path_ezvir}/EZV0.1-database.fa"

    output: f"{db_path_ezvir}/EZV0.1-database.1.bt2",
            f"{db_path_ezvir}/EZV0.1-database.2.bt2",
            f"{db_path_ezvir}/EZV0.1-database.3.bt2",
            f"{db_path_ezvir}/EZV0.1-database.4.bt2",
            f"{db_path_ezvir}/EZV0.1-database.rev.1.bt2",
            f"{db_path_ezvir}/EZV0.1-database.rev.2.bt2"

    params: f"{db_path_ezvir}/EZV0.1-database"

    log: logging_folder + "ezVIR/indexing.log"

    threads: 30

    shell: "bowtie2-build --threads {threads} -f {input} {params} &> {log}"

from datetime import date
today=date.today()

rule download_acc2taxid:
    output: temp(f"{db_path_ezvir}/taxonomy/nucl_gb.accession2taxid.gz")

    log: f"{db_path_ezvir}/taxonomy/acc2taxid_{today}_log.txt"

    shell:
        """
        wget ftp://ftp.ncbi.nih.gov/pub/taxonomy/accession2taxid/nucl_gb.accession2taxid.gz -O {output} 2> {log}
        """

rule decompress_acc2taxid:
    input: f"{db_path_ezvir}/taxonomy/nucl_gb.accession2taxid.gz"

    output: f"{db_path_ezvir}/taxonomy/nucl_gb.accession2taxid"

    shell:
        """
        zcat {input} > {output}
        """

rule genrate_ezVIR_required_files:
    conda: pipeline_path + "envs/ezvir.yml"

    input: fna=f"{db_path_ezvir}/EZV0.1-database.fa",
            acc2taxid=f"{db_path_ezvir}/taxonomy/nucl_gb.accession2taxid"

    output: len=f"{db_path_ezvir}/genome_lengths.csv",
            names=f"{db_path_ezvir}/genome_names.csv",
            taxids=f"{db_path_ezvir}/genome_taxids.csv",
            colours=f"{db_path_ezvir}/colours.csv"

    params: mail=config["email"],
            api_key=config["api_key"]

    log: f'{db_path_ezvir}/entries-info.log'

    script: "required_files.py"



