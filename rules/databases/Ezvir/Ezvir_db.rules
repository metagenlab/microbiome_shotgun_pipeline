db_path_ezvir=config["ezvir_database"]

def aggregate_input(wildcards):
    checkpoint_output = checkpoints.write_all_fasta.get(**wildcards).output[0]
    return expand(db_path_ezvir + "index/{i}.1.bt2",
           i=glob_wildcards(os.path.join(checkpoint_output, "{i}.fa")).i)


rule all_ezvir_db:
        input:  aggregate_input,
                "Ezvir/genome_lengths.csv",
                "Ezvir/genome_names.csv"


rule decompress_database:
    input: db_path_ezvir + "EZV0.1-database.fasta.gz"

    output: db_path_ezvir + "EZV0.1-database.fa"

    shell: "zcat {input} > {output}"


import os
checkpoint write_all_fasta:
    conda: pipeline_path + "envs/biopython-pandas.yml"

    input: db_path_ezvir + "EZV0.1-database.fa"

    output: directory(os.path.join(db_path_ezvir,"index"))

    script: "write_all_fasta.py"



checkpoint genrate_required_files:
    conda: pipeline_path + "envs/biopython-pandas.yml"

    input: db_path_ezvir + "EZV0.1-database.fa"

    output: "Ezvir/genome_lengths.csv",
            "Ezvir/genome_names.csv"

    script: "required_files.py"



rule index_database:
    conda: pipeline_path + "envs/ezvir.yml"

    input:  db_path_ezvir + "index/{genome_id}.fa"

    output: db_path_ezvir + "index/{genome_id}.1.bt2",
            db_path_ezvir + "index/{genome_id}.2.bt2",
            db_path_ezvir + "index/{genome_id}.3.bt2",
            db_path_ezvir + "index/{genome_id}.4.bt2",
            db_path_ezvir + "index/{genome_id}.rev.1.bt2",
            db_path_ezvir + "index/{genome_id}.rev.2.bt2"

    params: db_path_ezvir + "index/{genome_id}"

    log:
        logging_folder + "Ezvir/{genome_id}/indexing.log"

    threads: 1

    shell: "bowtie2-build --threads {threads} -f {input} {params} &> {log}"