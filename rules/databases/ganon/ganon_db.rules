ganon_db_path=config["ganon_db_path"]

rule all_refseqCG:
    input:  dl=f"{ganon_db_path}/refseqCG/assemblies/updated_sequence_accession.txt",
            filter=f"{ganon_db_path}/refseqCG/refseqCG.filter",
            fna=f"{ganon_db_path}/refseqCG/refseqCG.fna",
            names=f"{ganon_db_path}/refseqCG/assemblies/names.dmp",
            nodes=f"{ganon_db_path}/refseqCG/assemblies/nodes.dmp",
            merged=f"{ganon_db_path}/refseqCG/assemblies/merged.dmp",
            seq_info=f"{ganon_db_path}/refseqCG/assemblies/acc_len_taxid.txt"


rule download_fna_refseqCG:
    conda: pipeline_path + "envs/ganon.yml"

    output: f"{ganon_db_path}/refseqCG/assemblies/updated_sequence_accession.txt",
            f"{ganon_db_path}/refseqCG/assemblies/taxdump.tar.gz"


    params: f"{ganon_db_path}/refseqCG"

    threads: 48

    log: logging_folder + "ganon/database/download-refseqCG.log"

    shell:
        """
        wget_tries=10 wget_timeout=200 
        genome_updater.sh -d "refseq" -g "viral,archaea,bacteria" -c "all" -l "Complete Genome" -o {params} -a -u -r -m -b assemblies -f "genomic.fna.gz,assembly_report.txt" -t {threads} &> {log}
        genome_updater.sh -d "refseq" -g "viral,archaea,bacteria" -c "all" -l "Complete Genome" -o {params} -i -a -u -r -m -b assemblies -f "genomic.fna.gz,assembly_report.txt" -t {threads} &>> {log}
        """



rule merge_fna_refseqCG:
    input: f"{ganon_db_path}/refseqCG/assemblies/updated_sequence_accession.txt"

    output: temp(f"{ganon_db_path}/refseqCG/refseqCG.fna")

    params: fna=f"{ganon_db_path}/refseqCG/assemblies/files"

    shell:
        """
        find {params} -name '*.fna.gz' -print0 | xargs -0 zcat >> {output}
        """

rule decompress_taxdump_refseqCG:
    input: f"{ganon_db_path}/refseqCG/assemblies/taxdump.tar.gz"

    output: f"{ganon_db_path}/refseqCG/assemblies/names.dmp",
            f"{ganon_db_path}/refseqCG/assemblies/nodes.dmp",
            f"{ganon_db_path}/refseqCG/assemblies/merged.dmp"

    params: f"{ganon_db_path}/refseqCG/assemblies"

    shell:
        """
        tar xzf {input} -C {params}
        """


rule get_seq_info:
    input: f"{ganon_db_path}/refseqCG/assemblies/updated_sequence_accession.txt"

    output: f"{ganon_db_path}/refseqCG/assemblies/acc_len_taxid.txt"

    params: f"{workflow.basedir}/rules/databases/ganon/get_acc_len.sh"

    shell:
        """
        bash {params} {input} {output}
        """

rule ganon_build_index:
    conda: pipeline_path + "envs/ganon.yml"

    input:  fna=f"{ganon_db_path}/refseqCG/refseqCG.fna",
            names=f"{ganon_db_path}/refseqCG/assemblies/names.dmp",
            nodes=f"{ganon_db_path}/refseqCG/assemblies/nodes.dmp",
            merged=f"{ganon_db_path}/refseqCG/assemblies/merged.dmp",
            seq_info=f"{ganon_db_path}/refseqCG/assemblies/acc_len_taxid.txt"

    output: f"{ganon_db_path}/refseqCG/refseqCG.filter"

    params: prefix=f"{ganon_db_path}/refseqCG/refseqCG"

    threads: 48

    log: logging_folder + "ganon/database/refseqCG-index.log"

    shell:
        """
        ganon build -d {params.prefix} -i {input.fna} -t {threads} --taxdump-file {input.nodes} {input.names} {input.merged} --seq-info-file {input.seq_info} &> {log}
        """
