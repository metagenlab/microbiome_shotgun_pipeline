rule all_ganon:
    input: expand("ganon/{sample}/classified.rep",sample=list(read_naming.keys()))

ganon_db_path=config["ganon_db_path"]
host_acc=config['reference_genome']['filename']
job_mem_alloc=config["job_memory_limit"]

sample_suffix=list(read_naming.values())
if len(sample_suffix[0])==2:
    paired=True
else:
    paired=False

if paired==True:
    rule ganon_PE_reads:
        conda: pipeline_path + "envs/ganon.yml"

        input: r1=f"decontaminated-reads/{host_acc}/{{sample}}_R1.fq",
               r2=f"decontaminated-reads/{host_acc}/{{sample}}_R2.fq"

        output: classified="ganon/{sample}/classified.out",
                unclassified="ganon/{sample}/unclassified.txt",
                report="ganon/{sample}/classified.rep"

        params: f"{ganon_db_path}"

        threads: config["num_cores"]

        log: logging_folder + "ganon/classify/{sample}.log"

        benchmark: "benchmark/resources_usage/{sample}/ganon.benchmark.txt"

        resources: mem_gb=job_mem_alloc

        shell:
             """
             ganon classify -d {params} -p {input.r1} {input.r2} -t {threads} -o ganon/{wildcards.sample}/classified \
             -n {output.unclassified} --verbose &>> {log}
             """

else:
    rule ganon_SE_reads:
        conda: pipeline_path + "envs/ganon.yml"

        input: r1=f"decontaminated-reads/{host_acc}/{{sample}}_single.fq"

        output: classified="ganon/{sample}/classified.out",
                unclassified="ganon/{sample}/unclassified.txt",
                report="ganon/{sample}/classified.rep"

        params: f"{ganon_db_path}"

        threads: config["num_cores"]

        log: logging_folder + "ganon/classify/{sample}.log"

        benchmark: "benchmark/resources_usage/{sample}/ganon.benchmark.txt"

        resources: mem_gb=job_mem_alloc

        shell:
             """
             ganon classify -d {params} -r {input.r1} -t {threads} -o ganon/{wildcards.sample}/classified \
             -n {output.unclassified} --verbose &>> {log}
             """



